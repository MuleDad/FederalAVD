param appUpdateUserAssignedIdentityResourceId string
param availability string
param azureBackupPrivateDnsZoneResourceId string
param azureBlobPrivateDnsZoneResourceId string
param azureFilePrivateDnsZoneResourceId string
param azureQueuePrivateDnsZoneResourceId string
param deploymentUserAssignedIdentityClientId string
param deploymentVirtualMachineName string
@secure()
param domainJoinUserPassword string
@secure()
param domainJoinUserPrincipalName string
param domainName string
param fslogixEncryptionKeyNameConv string
param encryptionKeyVaultUri string
param encryptionUserAssignedIdentityResourceId string
param fileShares array
param hostPoolResourceId string
param identitySolution string
param kerberosEncryptionType string
param keyManagementStorageAccounts string
param location string
param logAnalyticsWorkspaceId string
param ouPath string
param privateEndpoint bool
param privateEndpointNameConv string
param privateEndpointNICNameConv string
param privateEndpointSubnetResourceId string
param recoveryServices bool
param recoveryServicesVaultName string
param deploymentResourceGroupName string
param resourceGroupStorage string
param shardingOptions string
param shareAdminGroups array
param shareSizeInGB int
param shareUserGroups array
param storageAccountNamePrefix string
param storageCount int
param storageIndex int
param storageSku string
param tags object
param deploymentSuffix string
param timeZone string

var adminRoleDefinitionId = '69566ab7-960f-475b-8e7c-b3118f30c6bd' // Storage File Data Privileged Contributor

var defaultSharePermission = 'StorageFileDataSmbShareContributor'

var privateEndpointVnetName = !empty(privateEndpointSubnetResourceId) && privateEndpoint
  ? split(privateEndpointSubnetResourceId, '/')[8]
  : ''

var privateEndpointVnetId = length(privateEndpointVnetName) < 37
  ? privateEndpointVnetName
  : uniqueString(privateEndpointVnetName)

var smbMultiChannel = {
  multichannel: {
    enabled: true
  }
}
var smbSettings = {
  versions: 'SMB3.0;SMB3.1.1;'
  authenticationMethods: 'NTLMv2;Kerberos;'
  kerberosTicketEncryption: kerberosEncryptionType == 'RC4' ? 'RC4-HMAC;' : 'AES-256;'
  channelEncryption: 'AES-128-CCM;AES-128-GCM;AES-256-GCM;'
}
var storageRedundancy = availability == 'availabilityZones' ? '_ZRS' : '_LRS'

var backupPrivateDNSZoneResourceIds = [
  azureBackupPrivateDnsZoneResourceId
  azureBlobPrivateDnsZoneResourceId
  azureQueuePrivateDnsZoneResourceId
]

var nonEmptyBackupPrivateDNSZoneResourceIds = filter(backupPrivateDNSZoneResourceIds, zone => !empty(zone))


resource storageAccounts 'Microsoft.Storage/storageAccounts@2022-09-01' = [
  for i in range(0, storageCount): {
    name: '${storageAccountNamePrefix}${string(padLeft(i + storageIndex, 2, '0'))}'
    kind: storageSku == 'Standard' ? 'StorageV2' : 'FileStorage'
    location: location
    identity: keyManagementStorageAccounts != 'MicrosoftManaged'
      ? {
          type: 'UserAssigned'
          userAssignedIdentities: {
            '${encryptionUserAssignedIdentityResourceId}': {}
          }
        }
      : null
    properties: {
      accessTier: 'Hot'
      allowBlobPublicAccess: false
      allowCrossTenantReplication: false
      allowedCopyScope: privateEndpoint ? 'PrivateLink' : 'AAD'
      allowSharedKeyAccess: identitySolution == 'EntraId' ? true : false
      azureFilesIdentityBasedAuthentication: identitySolution != 'EntraId' && !contains(
          identitySolution,
          'EntraKerberos'
        )
        ? {
            defaultSharePermission: defaultSharePermission
            directoryServiceOptions: identitySolution == 'EntraDomainServices' ? 'AADDS' : 'None'
          }
        : null
      defaultToOAuthAuthentication: false
      dnsEndpointType: 'Standard'
      encryption: {
        identity: keyManagementStorageAccounts != 'MicrosoftManaged'
          ? {
              userAssignedIdentity: encryptionUserAssignedIdentityResourceId
            }
          : null
        services: storageSku == 'Standard'
          ? {
              blob: {
                keyType: 'Account'
                enabled: true
              }
              file: {
                keyType: 'Account'
                enabled: true
              }
            }
          : {
              file: {
                keyType: 'Account'
                enabled: true
              }
            }
        keySource: keyManagementStorageAccounts != 'MicrosoftManaged' ? 'Microsoft.KeyVault' : 'Microsoft.Storage'
        keyvaultproperties: keyManagementStorageAccounts != 'MicrosoftManaged'
          ? {
              keyname: replace(fslogixEncryptionKeyNameConv, '##', padLeft(i + storageIndex, 2, '0'))
              keyvaulturi: encryptionKeyVaultUri
            }
          : null
        requireInfrastructureEncryption: true
      }
      largeFileSharesState: storageSku == 'Standard' ? 'Enabled' : null
      minimumTlsVersion: 'TLS1_2'
      networkAcls: {
        bypass: 'AzureServices'
        defaultAction: privateEndpoint ? 'Deny' : 'Allow'
      }
      publicNetworkAccess: privateEndpoint ? 'Disabled' : 'Enabled'
      sasPolicy: {
        expirationAction: 'Log'
        sasExpirationPeriod: '180.00:00:00'
      }
      supportsHttpsTrafficOnly: true
    }
    sku: {
      name: '${storageSku}${storageRedundancy}'
    }
    tags: union({ 'cm-resource-parent': hostPoolResourceId }, tags[?'Microsoft.Storage/storageAccounts'] ?? {})
  }
]

resource fileServices 'Microsoft.Storage/storageAccounts/fileServices@2022-09-01' = [
  for i in range(0, storageCount): {
    parent: storageAccounts[i]
    name: 'default'
    properties: {
      protocolSettings: {
        smb: storageSku == 'Standard' ? smbSettings : union(smbSettings, smbMultiChannel)
      }
      shareDeleteRetentionPolicy: {
        enabled: false
      }
    }
  }
]

module shares 'shares.bicep' = [
  for i in range(0, storageCount): {
    name: '${storageAccounts[i].name}-fileShares-${deploymentSuffix}'
    params: {
      fileShares: fileShares
      shareSizeInGB: shareSizeInGB
      StorageAccountName: storageAccounts[i].name
      storageSku: storageSku
    }
  }
]

module privateEndpoints '../../../../sharedModules/resources/network/private-endpoint/main.bicep' = [
  for i in range(0, storageCount): if (privateEndpoint) {
    name: '${storageAccounts[i].name}-privateEndpoint-${deploymentSuffix}'
    params: {
      customNetworkInterfaceName: replace(
        replace(replace(privateEndpointNICNameConv, 'SUBRESOURCE', 'file'), 'RESOURCE', '${storageAccounts[i].name}'),
        'VNETID',
        privateEndpointVnetId
      )
      groupIds: [
        'file'
      ]
      location: location
      name: replace(
        replace(replace(privateEndpointNameConv, 'SUBRESOURCE', 'file'), 'RESOURCE', '${storageAccounts[i].name}'),
        'VNETID',
        privateEndpointVnetId
      )
      privateDnsZoneGroup: empty(azureFilePrivateDnsZoneResourceId)
        ? null
        : {
            privateDNSResourceIds: [
              azureFilePrivateDnsZoneResourceId
            ]
          }
      serviceResourceId: storageAccounts[i].id
      subnetResourceId: privateEndpointSubnetResourceId
      tags: union(
        {
          'cm-resource-parent': hostPoolResourceId
        },
        tags[?'Microsoft.Network/privateEndpoints'] ?? {}
      )
    }
  }
]

resource storageAccounts_diagnosticSettings 'Microsoft.Insights/diagnosticSettings@2021-05-01-preview' = [
  for i in range(0, storageCount): if (!empty(logAnalyticsWorkspaceId)) {
    name: '${storageAccounts[i].name}-diagnosticSettings'
    properties: {
      metrics: [
        {
          category: 'Transaction'
          enabled: true
        }
      ]
      workspaceId: logAnalyticsWorkspaceId
    }
    scope: storageAccounts[i]
  }
]

resource storageAccounts_file_diagnosticSettings 'Microsoft.Insights/diagnosticSettings@2021-05-01-preview' = [
  for i in range(0, storageCount): if (!empty(logAnalyticsWorkspaceId)) {
    name: '${storageAccounts[i].name}-file-diagnosticSettings'
    scope: fileServices[i]
    properties: {
      workspaceId: logAnalyticsWorkspaceId
      logs: [
        {
          category: 'StorageDelete'
          enabled: true
        }
      ]
      metrics: [
        {
          category: 'Transaction'
          enabled: true
        }
      ]
    }
  }
]

// Assigns the Storage File Data Privileged Contributor role to the Storage Account for admins so they can adjust NTFS permissions if needed.
module roleAssignmentsAdmins '../../../../sharedModules/resources/storage/storage-account/rbac.bicep' = [
  for i in range(0, storageCount): if (!empty(shareAdminGroups)) {
    name: '${storageAccounts[i].name}-AdminRoleAssignments-${deploymentSuffix}'
    params: {
      principalIds: map(shareAdminGroups, group => group.id)
      principalType: 'Group'
      storageAccountResourceId: storageAccounts[i].id
      roleDefinitionId: adminRoleDefinitionId
    }
  }
]

module configureADDSAuth 'domainJoin.bicep' = if (identitySolution == 'ActiveDirectoryDomainServices') {
  name: 'Join-Domain-${deploymentSuffix}'
  scope: resourceGroup(deploymentResourceGroupName)
  params: {
    domainJoinUserPrincipalName: domainJoinUserPrincipalName
    domainJoinUserPassword: domainJoinUserPassword
    hostPoolName: last(split(hostPoolResourceId, '/'))
    kerberosEncryptionType: kerberosEncryptionType
    location: location
    ouPath: ouPath
    resourceGroupStorage: resourceGroupStorage
    storageAccountNamePrefix: storageAccountNamePrefix
    storageCount: storageCount
    storageIndex: storageIndex
    userAssignedIdentityClientId: deploymentUserAssignedIdentityClientId
    virtualMachineName: deploymentVirtualMachineName
  }
  dependsOn: [
    privateEndpoints
    shares
  ]
}
// Configure Entra Kerberos Hybrid with Domain Info if domainName, domainJoinUserPrincipalName and domainJoinUserPassword are provided. If they were, the deployment helper VM is domain joined. If not, then the deployment helper VM is not domain joined and can't run this configuration.
module configureEntraKerberosWithDomainInfo 'azureFilesEntraKerberosWithDomainInfo.bicep' = if (identitySolution == 'EntraKerberos-Hybrid' && !empty(domainName) && !empty(domainJoinUserPassword) && !empty(domainJoinUserPrincipalName)) {
  name: 'Configure-Entra-Kerberos-DomainInfo-${deploymentSuffix}'
  scope: resourceGroup(deploymentResourceGroupName)
  params: {
    defaultSharePermission: defaultSharePermission
    domainJoinUserPrincipalName: domainJoinUserPrincipalName
    domainJoinUserPassword: domainJoinUserPassword
    location: location
    resourceGroupStorage: resourceGroupStorage
    storageAccountNamePrefix: storageAccountNamePrefix
    storageCount: storageCount
    storageIndex: storageIndex
    userAssignedIdentityClientId: deploymentUserAssignedIdentityClientId
    virtualMachineName: deploymentVirtualMachineName
  }
  dependsOn: [
    privateEndpoints
    shares
  ]
}

module configureEntraKerberosWithoutDomainInfo 'azureFilesEntraKerberosWithoutDomainInfo.bicep' = if (identitySolution == 'EntraKerberos-CloudOnly' || (identitySolution == 'EntraKerberos-Hybrid' && (empty(domainName) || empty(domainJoinUserPassword) || empty(domainJoinUserPrincipalName)))) {
  name: 'Configure-Entra-Kerberos-${deploymentSuffix}'
  params: {
    defaultSharePermission: defaultSharePermission
    location: location
    kind: storageSku == 'Standard' ? 'StorageV2' : 'FileStorage'
    skuName: '${storageSku}${storageRedundancy}'
    storageAccountNamePrefix: storageAccountNamePrefix
    storageCount: storageCount
    storageIndex: storageIndex
    // Pass additional properties to satisfy policy requirements
    sasExpirationPeriod: '180.00:00:00'
    allowSharedKeyAccess: identitySolution == 'EntraId' ? true : false
    allowBlobPublicAccess: false
    allowCrossTenantReplication: false
    allowedCopyScope: privateEndpoint ? 'PrivateLink' : 'AAD'
    publicNetworkAccess: privateEndpoint ? 'Disabled' : 'Enabled'
    networkAcls: {
      bypass: 'AzureServices'
      defaultAction: privateEndpoint ? 'Deny' : 'Allow'
    }
    minimumTlsVersion: 'TLS1_2'
    requireInfrastructureEncryption: true
    fslogixEncryptionKeyNameConv: fslogixEncryptionKeyNameConv
    largeFileSharesState: storageSku == 'Standard' ? 'Enabled' : 'Disabled'
    dnsEndpointType: 'Standard'
    encryption: keyManagementStorageAccounts != 'MicrosoftManaged' ? {
      identity: {
        userAssignedIdentity: encryptionUserAssignedIdentityResourceId
      }
      services: storageSku == 'Standard' ? {
        blob: {
          keyType: 'Account'
          enabled: true
        }
        file: {
          keyType: 'Account'
          enabled: true
        }
      } : {
        file: {
          keyType: 'Account'
          enabled: true
        }
      }
      keySource: 'Microsoft.KeyVault'
      keyvaultproperties: {
        keyname: replace(fslogixEncryptionKeyNameConv, '##', '00') // Placeholder, will be replaced per storage account
        keyvaulturi: encryptionKeyVaultUri
      }
    } : {}
    identity: keyManagementStorageAccounts != 'MicrosoftManaged' ? {
      type: 'UserAssigned'
      userAssignedIdentities: {
        '${encryptionUserAssignedIdentityResourceId}': {}
      }
    } : {}
    accessTier: 'Hot'
    tags: union({ 'cm-resource-parent': hostPoolResourceId }, tags[?'Microsoft.Storage/storageAccounts'] ?? {})
  }
  dependsOn: [
    privateEndpoints
    shares
  ]
}

// PHASE 1: Update application manifest with privatelink FQDNs and tags
// This must happen BEFORE NTFS permissions are set so authentication works through private endpoints
module updateStorageApplicationsManifest 'updateEntraIdStorageKerbAppsManifest.bicep' = if (((identitySolution == 'EntraKerberos-Hybrid' && privateEndpoint) || (identitySolution == 'EntraKerberos-CloudOnly')) && !empty(appUpdateUserAssignedIdentityResourceId)) {
  name: 'Update-Storage-App-Manifest-${deploymentSuffix}'
  scope: resourceGroup(deploymentResourceGroupName)
  params: {
    appDisplayNamePrefix: '[Storage Account] ${storageAccountNamePrefix}'
    enableCloudGroupSids: identitySolution == 'EntraKerberos-CloudOnly' ? true : false
    location: location
    privateEndpoint: privateEndpoint
    userAssignedIdentityResourceId: appUpdateUserAssignedIdentityResourceId
    virtualMachineName: deploymentVirtualMachineName
  }
  dependsOn: [
    privateEndpoints
    shares
    configureEntraKerberosWithDomainInfo
    configureEntraKerberosWithoutDomainInfo
  ]
}

module SetNTFSPermissions 'setNTFSPermissionsAzureFiles.bicep' = {
  name: 'Set-NTFS-Permissions-${deploymentSuffix}'
  scope: resourceGroup(deploymentResourceGroupName)
  params: {
    location: location
    shardingOptions: shardingOptions
    shares: fileShares
    storageAccountNamePrefix: storageAccountNamePrefix
    storageCount: storageCount
    storageIndex: storageIndex
    userAssignedIdentityClientId: deploymentUserAssignedIdentityClientId
    userGroups: identitySolution == 'EntraKerberos-CloudOnly' && !empty(appUpdateUserAssignedIdentityResourceId) ? map(shareUserGroups, group => group.id) : !empty(domainJoinUserPassword) && !empty(domainJoinUserPrincipalName) ? map(shareUserGroups, group => group.name) : []
    virtualMachineName: deploymentVirtualMachineName
  }
  dependsOn: [
    privateEndpoints
    shares
    configureEntraKerberosWithDomainInfo
    configureEntraKerberosWithoutDomainInfo
    configureADDSAuth
    updateStorageApplicationsManifest
  ]
}

// PHASE 2: Grant admin consent to storage account applications
// This must happen AFTER NTFS permissions are set
module grantStorageApplicationsConsent 'grantEntraIdStorageKerbAppsConsent.bicep' = if (((identitySolution == 'EntraKerberos-Hybrid' && privateEndpoint) || (identitySolution == 'EntraKerberos-CloudOnly')) && !empty(appUpdateUserAssignedIdentityResourceId)) {
  name: 'Grant-Storage-App-Consent-${deploymentSuffix}'
  scope: resourceGroup(deploymentResourceGroupName)
  params: {
    appDisplayNamePrefix: '[Storage Account] ${storageAccountNamePrefix}'
    location: location
    userAssignedIdentityResourceId: appUpdateUserAssignedIdentityResourceId
    virtualMachineName: deploymentVirtualMachineName
  }
  dependsOn: [
    SetNTFSPermissions
  ]
}

module recoveryServicesVault '../../../../sharedModules/resources/recovery-services/vault/main.bicep' = if (recoveryServices) {
  name: 'RecoveryServices-AzureFiles-${deploymentSuffix}'
  scope: resourceGroup(resourceGroupStorage)
  params: {
    location: location
    name: recoveryServicesVaultName
    backupPolicies: [
      {
        name: 'filesharepolicy'
        type: 'Microsoft.RecoveryServices/vaults/backupPolicies'
        properties: {
          backupManagementType: 'AzureStorage'
          workloadType: 'AzureFileShare'
          schedulePolicy: {
            schedulePolicyType: 'SimpleSchedulePolicy'
            scheduleRunFrequency: 'Daily'
            scheduleRunTimes: [
              '23:00'
            ]
          }
          retentionPolicy: {
            retentionPolicyType: 'LongTermRetentionPolicy'
            dailySchedule: {
              retentionTimes: [
                '23:00'
              ]
              retentionDuration: {
                count: 30
                durationType: 'Days'
              }
            }
          }
          timeZone: timeZone
          workLoadType: 'AzureFileShare'
        }
      }
    ]
    diagnosticWorkspaceId: logAnalyticsWorkspaceId
    privateEndpoints: privateEndpoint && !empty(privateEndpointSubnetResourceId)
      ? [
          {
            customNetworkInterfaceName: replace(
              replace(
                replace(privateEndpointNICNameConv, 'SUBRESOURCE', 'azurebackup'),
                'RESOURCE',
                recoveryServicesVaultName
              ),
              'VNETID',
              '${split(privateEndpointSubnetResourceId, '/')[8]}'
            )
            name: replace(
              replace(
                replace(privateEndpointNameConv, 'SUBRESOURCE', 'azurebackup'),
                'RESOURCE',
                recoveryServicesVaultName
              ),
              'VNETID',
              '${split(privateEndpointSubnetResourceId, '/')[8]}'
            )
            privateDnsZoneGroup: empty(nonEmptyBackupPrivateDNSZoneResourceIds)
              ? null
              : {
                  privateDNSResourceIds: nonEmptyBackupPrivateDNSZoneResourceIds
                }
            service: 'AzureBackup'
            subnetResourceId: privateEndpointSubnetResourceId
            tags: union({ 'cm-resource-parent': hostPoolResourceId }, tags[?'Microsoft.Network/privateEndpoints'] ?? {})
          }
        ]
      : null
    protectionContainers: [
      for i in range(0, storageCount): {
        name: 'storagecontainer;Storage;${resourceGroupStorage};${storageAccounts[i].name}'
        friendlyName: storageAccounts[i].name
        sourceResourceId: storageAccounts[i].id
        backupManagementType: 'AzureStorage'
        containerType: 'StorageContainer'
        location: location
        protectedItems: [
          {
            name: 'AzureFileShare;${fileShares[0]}'
            policyId: '${resourceGroup().id}/providers/Microsoft.RecoveryServices/vaults/${recoveryServicesVaultName}/backupPolicies/filesharepolicy'
            protectedItemType: 'AzureFileShareProtectedItem'
            sourceResourceId: storageAccounts[i].id
          }
        ]
      }
    ]
    publicNetworkAccess: privateEndpoint ? 'Disabled' : 'Enabled'
    tags: union({ 'cm-resource-parent': hostPoolResourceId }, tags[?'Microsoft.RecoveryServices/vaults'] ?? {})
  }
}

output storageAccountResourceIds array = [for i in range(0, storageCount): storageAccounts[i].id]
